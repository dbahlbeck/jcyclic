buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.0'
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = 'se.cyclic.jcyclic'
version = '0.1.4'

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java


            artifact sourcesJar
            artifact javadocJar
        }
    }
}
compileJava {
    targetCompatibility = 1.7
    sourceCompatibility = 1.7

}

bintray {
    user = 'wursley'
    key = bintrayApiKey
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = 'jcyclic'
        desc = 'A Java API for finding cyclic dependencies'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/wursley/jcyclic.git'
        websiteUrl = 'https://github.com/wursley/jcyclic.git'
        issueTrackerUrl = 'https://github.com/wursley/jcyclic/issues'
        labels = ['java', 'dependencies', 'cycles', 'packages', 'classes']
    }
}

test {
    useJUnit()
}

repositories {
    jcenter()
}

dependencies {
    testCompile 'junit:junit:4.12'
    compile 'com.google.guava:guava:18.0'
    compile 'commons-io:commons-io:2.4'
    compile 'org.apache.bcel:bcel:5.2'
    compile 'org.jgrapht:jgrapht-core:0.9.0'
}